#!/usr/bin/env zsh

function senv() {
  local venv_path=""
  local dir=""
  local venv_names=(".venv" "venv")

  if [[ -n "$1" && -d "$1" ]]; then
    if [[ -f "$1/bin/activate" ]]; then
      venv_path="$1"
    else
      echo "ğŸ Provided directory is not a valid Python virtual environment: $1"
      return 1
    fi
  else
    dir=$PWD
    while [[ "$dir" != "/" ]]; do
      for name in $venv_names; do
        if [[ -f "$dir/$name/bin/activate" ]]; then
          venv_path="$dir/$name"
          break 2
        fi
      done
      for candidate in "$dir"/*(N); do
        if [[ -f "$candidate/pyvenv.cfg" && -f "$candidate/bin/activate" ]]; then
          venv_path="$candidate"
          break 2
        fi
      done
      dir=$(dirname "$dir")
    done
  fi

  if [[ -n "$venv_path" ]]; then
    if [[ -n "$VIRTUAL_ENV" ]]; then
      echo "ğŸ Environment already active: $VIRTUAL_ENV"
    else
      source "$venv_path/bin/activate"
      echo "ğŸ Activated: $venv_path"
    fi
  else
    echo "ğŸ No virtual environment found."
    return 1
  fi
}

senv "$@"
